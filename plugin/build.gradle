plugins {
    id 'java'
    id("xyz.jpenilla.run-paper")
}

dependencies {
    implementation project(":common")
    compileOnly("io.papermc.paper:paper-api:1.21.10-R0.1-SNAPSHOT")
    implementation("net.luckperms:api:5.4")
}

tasks.named("runServer") {
    minecraftVersion("1.21.10")
}

tasks.named("jar") {
    enabled = false
}

tasks.register("shadowJar", Jar) {
    dependsOn(":common:jar")
    archiveClassifier.set("")
    duplicatesStrategy = DuplicatesStrategy.EXCLUDE
    from(sourceSets.main.output)
    from({
        configurations.runtimeClasspath.filter { it.name.endsWith("jar") }.collect { zipTree(it) }
    })
}

tasks.named("processResources", Copy) {
    def props = [version: project.version]
    inputs.properties(props)
    filteringCharset = 'UTF-8'
    filesMatching('paper-plugin.yml') {
        expand(props)
    }
}

tasks.named("build") {
    dependsOn(tasks.named("shadowJar"))
}
