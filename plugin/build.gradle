plugins {
    id 'java'
    id("xyz.jpenilla.run-paper")
}

dependencies {
    implementation project(":common")
    compileOnly("io.papermc.paper:paper-api:1.21.11-R0.1-SNAPSHOT")
    compileOnly("net.luckperms:api:5.4")
    compileOnly("de.maxhenkel.voicechat:voicechat-api:2.5.0")
    implementation("com.github.retrooper:packetevents-spigot:2.11.0")
    testCompileOnly("io.papermc.paper:paper-api:1.21.11-R0.1-SNAPSHOT")
    testImplementation(platform("org.junit:junit-bom:5.10.2"))
    testImplementation("org.junit.jupiter:junit-jupiter")
    testImplementation("org.assertj:assertj-core:3.26.3")
    testImplementation("org.mockito:mockito-core:5.13.0")
    testImplementation("org.mockito:mockito-junit-jupiter:5.13.0")
    testRuntimeOnly("org.junit.platform:junit-platform-launcher")
    testRuntimeOnly("io.papermc.paper:paper-api:1.21.11-R0.1-SNAPSHOT")
    testRuntimeOnly("com.mojang:authlib:1.5.25")
}

tasks.named("runServer") {
    minecraftVersion("1.21.11")
}

tasks.named("jar") {
    enabled = false
}

tasks.register("shadowJar", Jar) {
    dependsOn(":common:jar")
    archiveClassifier.set("")
    duplicatesStrategy = DuplicatesStrategy.EXCLUDE
    from(sourceSets.main.output)
    from({
        configurations.runtimeClasspath.filter { it.name.endsWith("jar") }.collect { zipTree(it) }
    })
}

tasks.named("test", Test) {
    useJUnitPlatform()
}

tasks.named("processResources", Copy) {
    def props = [version: project.version]
    inputs.properties(props)
    filteringCharset = 'UTF-8'
    filesMatching('paper-plugin.yml') {
        expand(props)
    }
}

tasks.named("build") {
    dependsOn(tasks.named("shadowJar"))
}
